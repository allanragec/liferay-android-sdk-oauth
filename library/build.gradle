apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'signing'

archivesBaseName = 'liferay-android-oauth'
group = 'com.liferay.mobile'
version = '1.2.0'
description = 'Liferay Android OAuth'

android {
	compileSdkVersion 21
	buildToolsVersion "21.1.1"

	defaultConfig {
		minSdkVersion 10
		targetSdkVersion 21
	}

	packagingOptions {
		exclude 'META-INF/LICENSE'
		exclude 'META-INF/NOTICE'
	}
}

configurations {
	all*.exclude group: 'org.apache.httpcomponents', module: 'httpcore'
}

dependencies {
	compile group: 'com.liferay.mobile', name: 'liferay-android-sdk', version: '6.2.0.19'
	compile group: 'com.squareup', name: 'otto', version: '1.3.5'
	compile group: 'oauth.signpost', name: 'signpost-commonshttp4', version: '1.2.1.2'
}

task javadoc(type: Javadoc) {
	source = android.sourceSets.main.java.srcDirs
}

task jarJavadoc(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

task jarSources(type: Jar) {
	classifier = 'sources'
	from android.sourceSets.main.java.srcDirs
}

artifacts {
	archives jarJavadoc
	archives jarSources
}

signing {
    sign configurations.archives
}

install {
	repositories.mavenInstaller {
		pom.project {
			name project.description
			description project.description
			url 'https://www.liferay.com/community/liferay-projects/liferay-mobile-sdk/overview'
			licenses {
				license {
					name 'LGPL 2.1'
					url 'http://opensource.org/licenses/lgpl-2.1.php'
					distribution 'repo'
				}
			}
			scm {
				url 'https://github.com/brunofarache/liferay-android-sdk-oauth'
				connection 'scm:git:git://github.com/brunofarache/liferay-android-sdk-oauth.git'
			}
			developers {
				developer {
					id 'brunofarache'
					name 'Bruno Farache'
					email 'bruno.farache@liferay.com'
				}
			}
		}
	}
}

task release(type: Copy, dependsOn: install) {
	from "$buildDir/outputs/aar"
	from "$buildDir/libs"
	from "$buildDir/poms"
	into "$buildDir/release"
	exclude 'library-debug.aar'

	rename { String name ->
		name.replace('library-release', "${archivesBaseName}-${version}")
	}

	rename { String name ->
		name.replace('pom-default.xml', "${archivesBaseName}-${version}.pom")
	}

	doLast {
		signing.sign file("build/release/${archivesBaseName}-${version}.pom")
	}
}