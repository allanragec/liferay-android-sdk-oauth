buildscript {
	repositories {
		mavenCentral()
	}

	dependencies {
		classpath 'com.android.tools.build:gradle:1.0.0'
	}
}

allprojects {
	repositories {
		mavenLocal()
		maven {
			url 'https://repository.liferay.com/nexus/content/groups/liferay-ce'
		}
		jcenter()
		mavenCentral()
	}

	configurations {
		formatter
	}

	dependencies {
		formatter group: 'com.liferay.portal', name: 'portal-impl', version: '6.2.0-ga1'
		formatter group: 'com.liferay.portal', name: 'portal-service', version: '6.2.0-ga1'
		formatter group: 'com.liferay.portal', name: 'util-java', version: '6.2.0-ga1'
		formatter group: 'dom4j', name: 'dom4j', version: '1.6.1'
		formatter group: 'org.apache.ant', name: 'ant', version: '1.8.2'
	}

	task format(type: JavaExec) {
		main = 'com.liferay.portal.tools.sourceformatter.SourceFormatter'
		classpath = configurations.formatter
		workingDir = 'src'
	}
}

configure(subprojects.findAll { it.name.startsWith('sample') }) {
	apply plugin: 'com.android.application'

	android {
		compileSdkVersion 21
		buildToolsVersion '21.1.1'

		packagingOptions {
			exclude 'META-INF/DEPENDENCIES'
			exclude 'META-INF/LICENSE'
			exclude 'META-INF/NOTICE'
		}

		defaultConfig {
			minSdkVersion 21
			targetSdkVersion 21
			versionCode 1
			versionName "1.0"
		}
	}

	dependencies {
		compile project(':library')
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.2.1'
}